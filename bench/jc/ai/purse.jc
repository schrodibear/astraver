
/* Jessie sample file: electronic Purse toy example */

type Purse = {		
  integer balance;
  invariant balance_non_negative(this) = this.balance >= 0;
}	


unit credit(Purse[0] this, integer s) 
  requires s >= 0;
  behavior amount_credited:
    assigns this.balance;
    ensures this.balance == \old(this.balance)+s;
{
    this.balance += s;
}

unit withdraw(Purse[0] this, integer s) 
  requires s >= 0 && s <= this.balance;
  behavior amount_withdrawn:
    assigns this.balance;
    ensures this.balance == \old(this.balance) - s;
{
  this.balance -= s;
}

integer test1(Purse[0] p1, Purse[0] p2)
  /* requires p1 != p2; */
  behavior result_is_nul:
    assigns p1.balance,p2.balance;
    ensures \result == 0;
{	
    p1.balance = 0;
    credit(p2,100);
    return p1.balance;
}

/* introducing exceptions */
    
exception NoCreditException of unit

unit withdraw2(Purse[0] this, integer s) 
  requires s >= 0;
  behavior amount_withdrawn:
     assigns this.balance;
     ensures s <= \old(this.balance) && 
	 this.balance == \old(this.balance) - s;
  behavior amount_too_large:
     throws NoCreditException; 
     assigns \nothing;
     ensures s > \old(this.balance);
{
    if (this.balance >= s) {
	this.balance -= s;
    }
    else {
	throw NoCreditException ();
    }
}	
 
boolean test2(Purse[0] p) 
  behavior ok_iff_enough_balance:
    assigns p.balance ;
    ensures \result <==> \old(p.balance) >= 1000;
{
    try {
	withdraw2(p,1000);
        return true;
    }
    catch NoCreditException e { 
	return false; 
    }
}

unit main()
  requires true;
  behavior main:
    ensures true;
{
    // Purse[1] p = new Purse[1];
    //    credit(p, 10);
    credit(null, 42);
}


/*
Local Variables: 
mode: java
compile-command: "make purse"
End: 
*/
